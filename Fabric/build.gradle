plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.28'
    include 'org.projectlombok:lombok:1.18.28'
    annotationProcessor('org.projectlombok:lombok:1.18.28')

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'
    include group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation "com.github.WaterMediaTeam.watermedia:build:$watermedia_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "watermedia_version", watermedia_version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "fabric_version", fabric_version
    inputs.property "loader_version", loader_version
    inputs.property "mod_id", mod_id
    inputs.property "mod_name", mod_name
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "minecraft_version": minecraft_version, "fabric_version": fabric_version, "loader_version": loader_version, "watermedia_version": watermedia_version, "mod_id": mod_id, "mod_name": mod_name
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName =  project.build_name
    withSourcesJar()
}